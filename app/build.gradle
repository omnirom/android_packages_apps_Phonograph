apply plugin: 'com.android.application'

repositories {
    flatDir{
        dirs 'src/main/libs/aar'
    }
}

def gitBranch() {
    def branch = ''
    def proc = 'git rev-parse --abbrev-ref HEAD'.execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.1'
    dataBinding {
        enabled = true
    }
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26

        renderscriptTargetApi 26
        vectorDrawables.useSupportLibrary = true

        applicationId 'org.omnirom.gramophone'
        versionCode 4
        versionName '1.0.3'
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'src/main/proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' DEBUG (' + gitBranch() + ')'
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    lintOptions {
        disable 'MissingTranslation'
        disable 'InvalidPackage'
        abortOnError false
    }
}

ext {
    supportLibVersion = '26.0.1'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'src/main/libs')

    compile "com.android.support:gridlayout-v7:$supportLibVersion"
    compile "com.android.support:cardview-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"
    compile "com.android.support:percent:$supportLibVersion"
    compile "com.android.support:preference-v7:$supportLibVersion"
    compile "com.android.support:preference-v14:$supportLibVersion"
    compile "com.android.support:palette-v7:$supportLibVersion"

    compile 'com.github.kabouzeid:app-theme-helper:1.3.4@aar'
    compile 'com.github.kabouzeid:RecyclerView-FastScroll:1.9-kmod@aar'
    compile 'com.github.kabouzeid:SeekArc:1.2-kmod@aar'
    compile 'com.github.kabouzeid:AndroidSlidingUpPanel:3.3.0-kmod3@aar'

    compile 'com.afollestad.material-dialogs:material-dialogs-core:0.9.4.2@aar'
    compile 'com.afollestad.material-dialogs:material-dialogs-commons:0.9.4.2@aar'
    compile 'com.afollestad:material-cab:0.1.12@aar'

    //noinspection GradleDynamicVersion
    compile 'de.psdev.licensesdialog:licensesdialog:1.8.1@aar'
    compile 'com.heinrichreimersoftware:material-intro:1.6@aar'
    compile 'me.zhanghai.android.materialprogressbar:material-progressbar-library:1.3.0@aar'
}

task copyDeps(type: Copy) {
    from configurations.compile
    into rootProject.rootDir.getAbsolutePath()+"/lib"
//rename can be applied on the files if you want to remove the version from the jar for instance
}
